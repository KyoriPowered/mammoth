plugins {
  alias libs.plugins.indra
  alias libs.plugins.indra.licenseHeader
  alias libs.plugins.indra.sonatype
}

group = "net.kyori"
version = "1.2.0-SNAPSHOT"
description = "Helpful API for writing Gradle plugins"

indraSonatype {
  useAlternateSonatypeOSSHost("s01")
}

dependencies {
  compileOnlyApi libs.jetbrainsAnnotations
  implementation gradleApi()
  testImplementation platform(libs.junit.bom)
  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine
}

tasks.withType(Test).configureEach {
  jvmArgs(["--add-opens=java.base/java.lang=ALL-UNNAMED"]) // for ProjectBuilder tests
}

allprojects {
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.license-header"
  apply plugin: "net.kyori.indra.publishing"

  indra {
    github("KyoriPowered", "mammoth") {
      ci true
      publishing true
    }
    mitLicense()

    configurePublications {
      pom {
        organization {
          name = "KyoriPowered"
          url = "https://kyori.net"
        }

        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
          developer {
            id = "zml"
            email = "zml at kyori [.] net"
            timezone = "America/Vancouver"
          }
        }
      }
    }
  }
}
